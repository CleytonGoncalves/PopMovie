apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "SNAPSHOT"

ext.minimumSdkVersion = 15

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.cleytongoncalves.popmovies"
        targetSdkVersion 23
        minSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode() // 150010203 = minApi 15, GL 0, MAJOR 01, MINOR 02, PATCH 03
        versionName generateVersionName() // 1.2.3-SNAPSHOT
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField("boolean", "ENABLE_DEBUG_TOOLS", "true")
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_KEY', '"7ca7df9023524265afdf4479d07c4ad0"'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName = versionName + "-" + ext.versionClassifier
    }
    return versionName;
}

//LoganSquare
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}
apply plugin: 'com.neenbedankt.android-apt'
//--

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.android.support:support-v4:23.2.0'

    //ButterKnife
    compile 'com.jakewharton:butterknife:7.0.1'
    //Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    //LoganSquare JSON
    apt 'com.bluelinelabs:logansquare-compiler:1.3.5'
    compile 'com.bluelinelabs:logansquare:1.3.5'
    compile 'com.github.aurae.retrofit2:converter-logansquare:1.3.0'
}
